plugins {
    id 'java'
}

group = 'com.github.JerichoFletcher'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation('org.slf4j:slf4j-api:1.7.+')
    implementation('org.slf4j:slf4j-log4j12:1.7.+')
    implementation("com.googlecode.json-simple:json-simple:1.1.1")

    implementation('net.dv8tion:JDA:5.0.0-beta.15') {
        exclude module: 'opus-java'
    }
}

jar {
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Main-Class': 'ebsi.EBSI'
        )
    }
}

tasks.wrapper {
    gradleVersion '8.3'
    distributionType Wrapper.DistributionType.ALL
}

tasks.register('run', JavaExec) {
    dependsOn classes
    mainClass = 'ebsi.EBSI'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('fat-jar', Jar) {
    manifest {
        attributes 'Main-Class': 'ebsi.EBSI'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnitPlatform()
}